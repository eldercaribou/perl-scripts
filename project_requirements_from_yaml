#!/usr/bin/env perl

# For support with this file, please file an issue on the GitHub issue tracker
# for this project: https://github.com/spicyjack/perl-scripts/issues

# TODO
# - Create a new View object that just prints the totals from each section;
# make it a Role that the other two View objects inherit
# - Change the output format of Digest mode, print a header with each section,
# then just the issue ID, issue type, and issue title after that, until the
# next section gets printed

=head1 NAME

B<project_requirements_from_yaml.pl> - Display on STDOUT, or create a
database, all of the project requirements given to the script via a YAML file.

=cut

our $copyright = q|Copyright (c)2014 Brian Manning|;

=head1 SYNOPSIS

 project_requirements_from_yaml [options]

 Script options:
 -d|--dump      Dump the YAML file after parsing, then exit
 -h|--help      Displays this help text
 -v|--verbose   Verbose script output
 -y|--yaml      YAML file to read from
 -o|--output    Create a database file with this filename
                Default is to write plaintext to STDOUT
 -g|--digest    Digest mode; print only ID and title of requirements
 -x|--overwrite Overwrite an existing database file

You can view the full C<POD> documentation of this file by calling
C<perldoc project_requirements_from_yaml>.

=cut

our @opts = (
    # script options
    q(dump|d),
    q(verbose|v),
    q(help|h),
    q(yaml|y=s),
    q(digest|g),
    q(type3|3),
    q(output|o=s),
    q(overwrite|x),
);

=head1 DESCRIPTION

Parses the contents of the input YAML file, and either outputs formatted
plaintext to C<STDOUT>, or creates a database file and populates it with the
data parsed from the YAML file.

=cut

### Pragmas
use strict;
use warnings;
use 5.010;

### View for printing all information for the project requirements
package App::Beastly::View::AllRequirements;
use Moo;

sub print_item {
    my $self = shift;
    my %args = @_;
    my $item = $args{item};
    my $total_items = $args{total_items};
    my $actual_time = $args{actual_time};

    say sprintf(q(ID: %04u: ), $total_items) . $item->{title};
    say "  Desc:        " . $item->{desc};
    say "  Created:     " . $item->{created};
    say "  Type:        " . $item->{type};
    say "  Time:        " . $item->{half_days} . q| (eѕtimate, in half-days)|;
    say "  Actual time: " . $args{actual_time};
    print qq(\n);
}

### View for printing only the ID and title of each project requirement
package App::Beastly::View::Digest;
use Moo;

sub print_item {
    my $self = shift;
    my %args = @_;
    my $item = $args{item};
    my $type = $item->{type};
    my $total_items = $args{total_items};
    say sprintf(q(ID %04u T%u: ), $total_items, $type) . $item->{title};
}

### View for printing only type 3 project requirements
package App::Beastly::View::Type3;
use Moo;

sub print_item {
    my $self = shift;
    my %args = @_;
    my $item = $args{item};
    my $total_items = $args{total_items};
    say sprintf(q(ID: %04u: ), $total_items) . $item->{title};
}

### Main package
package main;

### System modules
use File::Basename;
use Getopt::Long;
use Pod::Usage;
use YAML::XS qw(LoadFile);
use Data::Dumper;
$Data::Dumper::Indent = 1;
$Data::Dumper::Sortkeys = 1;
$Data::Dumper::Terse = 1;

    # for use in help/error output
    my $my_name = basename($0);

    # arguments parsed by Getopts
    my %args;
    my $parser = Getopt::Long::Parser->new();
    $parser->getoptions(\%args, @opts);

    if ( exists $args{help} ) { pod2usage(-exitstatus => 1); }

    if ( $args{dump} ) {
        say q(Dumping %args;);
        say Dumper %args;
    }

    # create a YAML object using the DATA filehandle
    # $struct should end up being a reference to an array of hashes
    my $yaml;
    if ( ! defined $args{yaml} ) {
        warn qq(Error: --yaml file to parse was not specified\n);
        die qq(Use '$my_name --help' to see script options\n);
    }
    if ( -e $args{yaml} ) {
        $yaml = LoadFile($args{yaml});
    } else {
        die q(Error: file ) . $args{yaml} . q( not found/unavailable);
    }

    if ( $args{dump} ) {
        say Dumper $yaml;
        exit 0;
    }

    my $view;
    if ( $args{digest} && $args{type3} ) {
        die qq(ERROR: use either --digest or --type3, not both\n);
    }
    if ( $args{digest} ) {
        $view = App::Beastly::View::Digest->new();
        say q(++++ Listing all issues in "digest" mode ++++);
        print qq(\n);
    } elsif ( $args{type3} ) {
        $view = App::Beastly::View::Type3->new();
        say q(++++ Listing all type 3 issues in "digest" mode ++++);
        print qq(\n);
    } else {
        $view = App::Beastly::View::AllRequirements->new();
    }

    my $total_items = 0;
    my $total_sections = 0;
    my $item_counter = 0;
    my $section_counter = 0;
    my $current_section;
    my $total_half_days = 0;
    my $computed_time = 0;
    my $actual_time = q();

    foreach my $item (@{$yaml}) {
        if ( ref($item) =~ q(HASH) ) {
            $item_counter++;
            $total_items++;
            if ( $item->{type} == 1 ) {
                $actual_time
                    = q|(Type 1 tasks don't receive any time adjustments)|;
                $computed_time = $item->{half_days};
            }
            if ( $item->{type} == 2 ) {
                $computed_time = $item->{half_days} * 1.5;
                $actual_time = q|(Type 2 task) |
                    . $item->{half_days}
                    . q( half-days * 1.5 = )
                    . $computed_time
                    . q| half-days total|;
            }
            if ( $item->{type} == 3 ) {
                $computed_time = $item->{half_days} * 3;
                $actual_time = q|(Type 3 task) |
                    . $item->{half_days}
                    . q( half-days * 3 = )
                    . $computed_time
                    . q| half-days total|;
            }
            # add the computed half-days to the total project time
            $total_half_days += $computed_time;
            # if we're printing 'type 3' issues only, check here and skip
            next if ( $args{type3} && $item->{type} != 3);
            $view->print_item(
                item => $item,
                total_items => $total_items,
                actual_time => $actual_time,
            );
        } else {
            # if we've already parsed another section print a section "footer"
            if ( defined $current_section ) {
                say qq(Total items in section "$current_section": )
                    . $item_counter;
                print qq(\n);
            }
            # then zero out the counters/section name, and start a new section
            $item_counter = 0;
            $total_sections++;
            $current_section = $item;
            say qq(---- Section: $current_section ----);
        }
    }
    # print one last section "footer"
    say qq(Total items in section "$current_section": $item_counter);
    print qq(\n);
    say qq(Total half-days required for project: )
        . sprintf(q(%0.2f), $total_half_days);
    say qq(Total number of project items: $total_items);
    say qq(Total number of project sections: $total_sections);

=head1 AUTHOR

Brian Manning, C<< <cpan at xaoc dot org> >>

=head1 BUGS

Please report any bugs or feature requests to the GitHub issue tracker for
this project:

C<< <https://github.com/spicyjack/perl-scripts/issues> >>.

=head1 SUPPORT

You can view documentation for this script with the C<perldoc> command.

    perldoc project_requirements_from_yaml

=head1 COPYRIGHT & LICENSE

Copyright (c) 2014 Brian Manning, all rights reserved.

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl itself.

=cut

# конец!
# vim: set shiftwidth=4 tabstop=4:
